# 10. I2C LCD 

- SCL : analog input  5번
- SDA : analog input 4번 잘 구분해서 꼽자

- void begin( ) : LCD를 시작한다
- int setCursor(col,row) : col과 rol에 위치로 이동
- void display() : 표시 on
- void blink() : 커서 위치를 깜빡인다.
- void scrollDisplayLeft() : 표시된 내용을 좌측으로 1칸이동
- void scrollDisplayRight() : 표시된 내용을 우측으로 1칸이동
- void createChar(uint8_t loc, uin8_t charmap[]) : uint8_t loc는 아싀코드값(0~7)출력, 
- void print() : 문자열, 문자 출력
- int write(uint8_t val) : 문자 출력

----

- LCD 기판에 표기 하기 전 Serial이 잘 출력되는지 (주소를 확인)

```c++
#include <Wire.h>

void setup() {
  Wire.begin();
  Serial.begin(9600);
  while (!Serial); // 	Leonardo: wait for serial monitor
  Serial.println("\nI2C Scanner");

}

void loop() {
  byte error, address;
  int nDevices;
  
  Serial.println("Scanning...");
  
  nDevices = 0;

   for(address = 1; address < 127; address++ ){
      Wire.beginTransmission(address);
      error = Wire.endTransmission();

      if (error == 0){
        Serial.print("I2C device found at address 0x");
        if (address<16) Serial.print("0");
        Serial.print(address,HEX);
        Serial.println(" !");
        nDevices++;
      }
       else if (error==4){
          Serial.print("Unknown error at address 0x");
          if (address<16) Serial.print("0");
          Serial.println(address,HEX);
      }
    }
    if (nDevices == 0) Serial.println("No I2C devices found\n");
    else Serial.println("done\n");
    
    delay(5000); // wait 5 seconds for next scan
}
```

- LiquidCrystal_I2C라는 내장함수를 통해 주소와 객체를 생성하여 LCD판에 불들어오게
  - LiquidCrysta_I2C는 [GitHub - fdebrabander/Arduino-LiquidCrystal-I2C-library: Library for the LiquidCrystal LCD display connected to an Arduino board.](https://github.com/fdebrabander/Arduino-LiquidCrystal-I2C-Library)
  - 에서 받으면 된다.(강의교안 4.3LCD 6page참고)

```c++
#include <LiquidCrystal_I2C.h>
LiquidCrystal_I2C myLCD(0x27, 16, 2); //(주소,col,row)껍데기 myLCD 객체 생성

void setup() {
  myLCD.begin(); // LCD 초기화
  
  myLCD.setCursor(0, 0); // 커서 홈(0,0)
  myLCD.print("I2C LCD Test!"); // 문자열 표시
  
  myLCD.setCursor(0, 1); // 커서 2행 1열(0, 1)
  myLCD.print("Hello, Arduino!"); // 문자열 표시


}

void loop() {

}
```

- Scroll을 통해 긴 문자열을 한칸씩 밀어서 보여주는 예제

```c++
#include <LiquidCrystal_I2C.h>
LiquidCrystal_I2C myLCD(0x27, 16, 2);

void setup() {
  // put your setup code here, to run once:
  int n;
  for(n = 0;n < 80;n++){
    myLCD.scrollDisplayLeft(); // 왼쪽으로 한 칸씩 이동
    delay(500);
  }
  
delay(4000); // 4초 대기

  for(n = 0;n < 80;n++){
    myLCD.scrollDisplayRight(); // 오른쪽으로 한 칸씩 이동
    delay(500);
  }
  
delay(4000); // 4초 대기
}

void loop() {
  // put your main code here, to run repeatedly:

}
```

- 픽셀 하나하나 위치기반 이진법으로 내 이름 출력해보기

```c++
#include <LiquidCrystal_I2C.h>
byte name_r[8] = {
        B11111,
        B00001,
        B11111,
        B10000,
        B11111,
        B11111,
        B01010,
        B01010
};

byte name_h[8] = {
        B01001,
        B11101,
        B11111,
        B10101,
        B11111,
        B00001,
        B10000,
        B11111
};

LiquidCrystal_I2C myLCD(0x27, 16, 2); // myLCD객체 생성

byte name_s[8] = {
        B00100,
        B01010,
        B10001,
        B00000,
        B11111,
        B00100,
        B00100,
        B00100
};


void setup() {
  myLCD.begin();
  myLCD.createChar(0, name_r);
  myLCD.createChar(1, name_h);
  myLCD.createChar(2, name_s);

}

void loop() {
  myLCD.setCursor(0,0); 
  myLCD.print("Hello, Arduino!");

  myLCD.setCursor(0,1);
  myLCD.print("My name is");

  myLCD.write(0);
  myLCD.write(1);
  myLCD.write(2);

}
```

### - 가변저항을 이용해 아날로그--> 디지털 변환으로 volt값 표시해주는 예제

```c++
const int var_pin = A0; // 가변저항 연결 핀
int analog_val;    // 아날로그 값 저장변수
#include <SimpleTimer.h>
SimpleTimer timer;

#include <LiquidCrystal_I2C.h>

LiquidCrystal_I2C myLCD(0x27, 16, 2); // myLCD 객체 생성

void check() {
  int digital_val;
  float ff;

  digital_val = analogRead(var_pin);  //아날로그 디지털 변환

  ff = (float)digital_val /1023. *5.0; // 전압계산
 // Lcd로 출력하며 출력모양 과 volt:4.3v, D.value:805
 // 문자열 클래스 : string
//  myLCD.clear();          // 덮어쓰기때문에 지워줘야하지만, 계속 깜빡임 그래서 자리수를 맞춰줘야
  
  char msg[17];
  
  String buf = "Volt: ";  // buf = "Volt : " +ff + "V";  -->에러
  buf = buf +ff +"V                    ";  //깜빡이는거 방지하기위해 스페이스 줌
  sprintf(msg,"%-16s", buf.c_str());   //- 붙이면 좌측정렬
//  sprintf(msg, "Volt: %f V",ff); // 이것은 안됨 아두이노의 sprintf는 float지원하지않음
  
  myLCD.setCursor(0,0);
//  myLCD.print(buf.c_str());
  myLCD.print(msg);



  buf = "D.value: ";    // buf = "D.value" + digital_vl; --> 에러
  buf = buf + digital_val;
//  sprintf(msg,"%-16s", buf.c_str());
  sprintf(msg, "D.value:%4d", digital_val);
  
  myLCD.setCursor(0,1);
//  myLCD.print(buf.c_str());
  myLCD.print(msg);

}

void setup(){
  Serial.begin(9600);
  myLCD.begin();
  timer.setInterval(1000,check);      // 0.1초마다 체크해줌
}

void loop() {
  timer.run();

}
```



-----

## MiniCom

- 0.1초 간격으로 작업하는 것!
- 값을 받아오는 것이라 생각하자
- MiniCom.h

```c++
#pragma once

#include <Arduino.h>
#include <LiquidCrystal_I2C.h>
#include <SimpleTimer.h>

class MiniCom{
protected:
    int serial_bps;  // 시리얼을 초기화하기위한것 받기
    LiquidCrystal_I2C lcd;  // 주소값 받기
    SimpleTimer timer;       // 디폴트

public:
    MiniCom(int serial_bps = 9600, int lcd_addr = 0x27);
    void init();
    int setInterval(unsigned long interval,timer_callback f); //Timer의 타입이 timer callback임
    void run();


    // LCD 출력 지원 메서드(helper함수)
    void print(int row, const char *pMsg, bool bserial = false); //시리얼에도 출력할꺼니? : false(No)
    void print_i(int row, String title, int value, bool bserial = false);
    void print_i(int row, String title1, int value1, String title2, int value2, bool bserial = false);
    void print_d(int row, String title, double value, bool bserial = false);

};
```

- MiniCom.cpp 

```c++
#include "MiniCom.h"

MiniCom::MiniCom(int serial_bps, int lcd_addr)
    :serial_bps(serial_bps),lcd(lcd_addr,16,2){

}

void MiniCom::init(){
    Serial.begin(serial_bps);
    lcd.begin();
    
}

int MiniCom::setInterval(unsigned long interval, timer_callback f){
    return timer.setInterval(interval, f);
}

void MiniCom::run(){
    timer.run();
}

void MiniCom::print(int row, const char *pMsg, bool bserial){
    char buf[17];
    sprintf(buf, "%-16s",pMsg);
    lcd.setCursor(0,row);
    lcd.print(buf);
    if(bserial){
        Serial.println(buf);
    }
}

void MiniCom::print_i(int row, String title, int value, bool bserial){
    String buf = title + value;
    print(row, buf.c_str(), bserial);

}

void MiniCom::print_i(int row, String title1, int value1,
                     String title2, int value2, bool bserial){
        
        String buf = title1 + value1 + "," + title2 + value2;
        print(row,buf.c_str(), bserial);

}

void MiniCom::print_d(int row, String title, double value, bool bserial){
    String buf = title + value;
    print(row, buf.c_str(), bserial);

}
```



- Minicom을 이용한 예제 Serial에 나오게 하기

```c++
#include <MiniCom.h>

MiniCom com;

void check(){                    // 0.1초간격으로 작업하는것
  Serial.print("check called\n");
}

void setup(){
  com.init();
  com.setInterval(100,check);
}


void loop() {
  com.run();

}
```

- MiniCom을 이용해 받아온다.값을! LCD에 출력

```c++
#include <MiniCom.h>
MiniCom com;

void check(){
   int digital_val = analogRead(A0);
   float ff = (float)digital_val /1023. *5.0;

   com.print_d(0,"Volt:",ff);
   com.print_i(1,"D.value:",digital_val,true); // 마지막인자 Serial false 가 디폴트라 생략가능
   // 마지막 Serial 나오게하고싶다면 마지막인자 추가하고 True하면 됨
   
 }
void setup() {
  com.init();
  com.setInterval(100,check);

}

void loop() {
  com.run();

}
```

