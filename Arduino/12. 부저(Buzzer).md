# 12. 부저(Buzzer)

- 능동부저(Active Buzzer)
  - 미리 설계된 회로를 가짐 -> 단일 음!! 을 가짐
    - 경고음으로 주로 활용
- 수동부저 (Passive Buzzer)
  - 자체회로를 가지고있찌않음
  - 출력 소리에 따른 주파수 신호 인가하면 해당소리 출력가능
  - 음계 표현 가능(멜로디 여주 가능)
  - void ton(pinNum, frequency, duration) : 핀번호, 주파수, 지속시간 나타냄
  - void noTone(pinNum) : 출력 정지

## 1) 능동부저(단일음)

- 수동부저필요 헤더파일 cpp 파일
- Buzzer.h

```c++
#pragma once
#include <Arduino.h>

class Buzzer{
protected:
    int pin;                // 핀번호
    int state;              // 소리의 출력상태
    int on_time;            // 소리를 내야하는 간격
    int off_time;           // 꺼져야 할 시간 간격
    unsigned long old_time; // 이전 상태 변화 시간
    bool bstop;

public:
    Buzzer(int pin);

    void run();
    void stop() { 
        bstop = true;
        digitalWrite(pin,LOW);   // 5넘어갈때 확실히 꺼줘야하기에 쓰는 녀석 
    }  //함수 짧을땐 여기 써도댐
    void setOffTime(int off_time); // 조정해야 하는 시간


};
```

- Buzzer.cpp

```c++
#include "Buzzer.h"

Buzzer::Buzzer(int pin): pin(pin){
    state = LOW;
    off_time =0;
    on_time = 100;   // 100ms
    old_time = millis();    // 3가지 방법
    pinMode(pin,OUTPUT);

}

void Buzzer::run() {

    unsigned long cur, diff;

    if(bstop) return;  // 5 넘어가게 하면 멈추게 해줌

    cur = millis();             // 현재 시간
    diff = cur - old_time;      // 이전 상태변화 시간에서의 시간 차

    if(state){    // 소리를 출력하고 있는 상태
        if (diff > on_time){   // 소리가 출력하고 있는 시간이 끝난다면!
            digitalWrite(pin,LOW); // 소리끄기
            state =LOW;     // 꺼짐 상태
            old_time =cur ; // 상태 변이 시간 갱신
        }
    }else{  // 소리가 꺼져있는 상태
        if(diff> off_time){ // 소리 꺼진 시간이 끝났을때
            digitalWrite(pin,HIGH); // 소리켜기
            state = HIGH;           // 켜짐상태
            old_time = cur;         // 상태 변이 시간 갱신
        }

    }
}

void Buzzer::setOffTime(int off_time){
    this ->off_time = off_time;
    bstop = false;
}
```

----

- buzzer 헤더파일 없이 그냥 연결한 예제 (완전 기본)

```c++
// 능동부저
const int buzzer_pin =2 ; 



void setup() {
  // put your setup code here, to run once:
  pinMode(buzzer_pin,OUTPUT);
}

void loop() {
  
  
//  digitalWrite(buzzer_pin,HIGH);
//  delay(2000);
//
//  digitalWrite(buzzer_pin,LOW);
//  delay(2000);
}
```

- 헤더파일을 이용해 가변저항값에 따른 부저 속도 조절 예제(중요)
  - 자동차 후진 경고등과 비슷함(가까워질수록 더 소리나는 주기가 짧아짐)

```c++
#include <MiniCom.h>
#include <Buzzer.h>

MiniCom com;
Buzzer buzzer(8);
unsigned long intervals[]{
  100, 200, 400, 700, 1000    /// interval 개념에 가져오는거 굉장히 중요하니 알아두자(중요함)
};


void check(){
  int value = analogRead(A0);
  int distance = map(value,0,1023,0,9); //0~1023까지 0부터 9로 나누겠다 map (내장)함수를쓰는게 중요! 
  
  com.print_i(0,"distance: ", distance);
  if(distance >= 5){
      buzzer.stop();
      com.print(1,"buzzer off");
    }else{
      unsigned long interval = intervals[distance]; // 0~9까지 순차적이기에 if나 switch 문을쓰지않고
      buzzer.setOffTime(interval);                  // 배열을 통해 접근 가능하다. (중요함)
      com.print_i(1,"off time: ",interval);
    }


  
}


void setup() {
  com.init();
  com.setInterval(100,check);

}

void loop() {
  com.run();
  buzzer.run();
}
```



---

## 수동부저(멜로디)

- 수동부저를 위해 멜로디 클래스/ 헤더파일, cpp를 정의해준다.
- Melody.h

```c++
#pragma once

#include <Arduino.h>

class Melody {
protected:
    int pin; //핀번호
    int *notes; // 음계배열
    int *durations; //박자배열
    int length; //음의개수
    int cur_ix; //현재 연주하는 음의 인덱스
    long old_time; // 이전시간
    int note_duration; // 현재 연주하는 음의 길이
    boolean b_play; // 연주 여부

public:
    Melody(int pin, int *notes, int *durations, int length);
    void stop();
    void play();
    void run();

};
```

- Melody.cpp

```c++
#include "Melody.h"

Melody::Melody(int pin, int *notes, int *durations, int length):
    pin(pin),notes(notes),durations(durations),length(length){

    pinMode(pin, OUTPUT);
    cur_ix = -1;
    note_duration =0;
    b_play = false;
    old_time = millis();       

}

void Melody::stop(){
    b_play = false;
}

void Melody::play(){
    b_play =true;
    cur_ix = -1;
    note_duration =0;
    old_time = millis();
}

void Melody::run(){
    if(!b_play) return;

    long current = millis();
    long diff = current - old_time;

    if(diff >=note_duration){
        int pauseBetweenNotes = note_duration * 0.3;
        delay(pauseBetweenNotes);

        cur_ix = (cur_ix+1)%length;
        note_duration = 1000 / durations[cur_ix];
        tone(pin, notes[cur_ix], note_duration);
        old_time = millis();
    }
}
```



----

- 곰세마리 가 한집에있어

```c++
#define NOTE_C4 262 // 4옥타브 도
#define NOTE_D4 294 // 4옥타브 레
#define NOTE_E4 330 // 4옥타브 미
#define NOTE_G4 392 // 4옥타브 솔
#define NOTE_A4 440 // 4옥타브 라
#define NOTE_C5 523 // 5옥타브 레

#define NUM 49

int melody[NUM] = { // 곰 세마리
  NOTE_C4, NOTE_C4, NOTE_C4, NOTE_C4, NOTE_C4, // 도도도도도
  NOTE_E4, NOTE_G4, NOTE_G4, NOTE_E4, NOTE_C4, // 미솔솔미도
  NOTE_G4, NOTE_G4, NOTE_E4, NOTE_G4, NOTE_G4, NOTE_E4, // 솔솔미솔솔미
  NOTE_C4, NOTE_C4, NOTE_C4, // 도도도
  
  NOTE_G4, NOTE_G4, NOTE_E4, NOTE_C4, // 솔솔미도
  NOTE_G4, NOTE_G4, NOTE_G4, // 솔솔솔
  NOTE_G4, NOTE_G4, NOTE_E4, NOTE_C4, // 솔솔미도
  NOTE_G4, NOTE_G4, NOTE_G4, // 솔솔솔
  NOTE_G4, NOTE_G4, NOTE_E4, NOTE_C4, // 솔솔미도
  NOTE_G4, NOTE_G4, NOTE_G4, NOTE_A4, NOTE_G4, // 솔솔솔라솔
  NOTE_C5, NOTE_G4, NOTE_C5, NOTE_G4, // 도솔도솔
  NOTE_E4, NOTE_D4, NOTE_C4}; // 미레도

// 음표 길이
int noteDuration[NUM] ={
  4, 8, 8, 4, 4, 4, 8, 8, 4, 4, 8, 8, 4, 8, 8, 4, 4, 4, 2,
  4, 4, 4, 4, 4, 4, 2, 4, 4, 4, 4, 4, 4, 2,
  4, 4, 4, 4, 8, 8, 8, 8, 2, 4, 4, 4, 4, 4, 4, 2};

const int speaker_pin = 2;
void setup() {
  pinMode(speaker_pin, OUTPUT);
}

void loop() {
  int m, d, dd;
  for(m = 0;m < NUM;m++){
    d = 1000 / noteDuration[m];
    dd = d* 1.3; // 음 출력 시간(4분 음표 325ms)
    tone(speaker_pin, melody[m], d);  // tone은 비동기 함수이다.노래끝날때 까지 기다려주지않는다
    delay(dd);  // 그래서 이 delay가 필요하다, d*1.3 주면 1박자마다 줘서 음 구분이 안된다, 0.3박여유시간을 줌
  }
  delay(10000);

}
```

- pitches.h와 pirates.h를 이용해음계를 구상하고 이를 통해 연주하는것

```c++
#include <Melody.h>			// 예는 연주해주는 기능만
#include "pitches.h"		// 이 안에 음계정보 다있음!
#include "pirates.h"		// 이 안에 음계정보가 다있음

int length = sizeof(notes)/sizeof(int);		// 음악길이 정해줌
Melody melody(2,notes,durations, length);

void setup() {
  melody.play();

}

void loop() {
  melody.run();

}
```

