# 18. 서보모터 & 키패드

## 1) 서보모터

- 회전각도 180도

- 서보모터 라이브러리
  - Servo 객채명   : 객체 생성
  - uint8_attach(int pin) : 객체에 핀 할당
  - void write(int value) : 객체 회전 각도로 회전시키기(각도 0~180)
  - int read()  :  마지막 write() 함수에서 사용한 값 읽기

## 2) 키패드

- 키패드 라이브러리가 존재하며(Github에 존재한다.)
  - Keypad 객체명   : 객체생성
  - char getKey(); 눌려진 값의 키를 리턴한다. 눌려지지 않았을 때는 NO_KEY 반환
  - void begin(makekeymap(userKeymap)) : 내부 키맵을 사용자맵과 똑같이 초기화한다.

---



- 서보모터 회전시키기(라이브러리 활용) + 조이스틱 사용

```c++
//서보모터 파형으로 조절하기
#include <Servo.h>
#include <MiniCom.h>

MiniCom com;
Servo myServo; // 사용할 서모 모터명 선언
const int servo_pin = 8;  // 서보 모터 핀

void control(){
  // 조이스틱의 x 값으로 서보 모터의 움직임 제어
  int value = analogRead(A1);
  
//  int angle = map(value,0,1023,0,180); // 맵함수로
//  myServo.write(angle);
//  com.print_i(0,"Angle: " ,angle-90);


//  또는
  int angle =map(value,0,1023,180,0);
  myServo.write(angle);
  com.print_i(0,"Angle: ", -(angle-90));
}

void setup() {
  myServo.attach(servo_pin); // 사용할 서보명에 핀 할당
//  myServo.write(90);        // 일단 90도로 고정
  com.init();
  com.setInterval(100,control);
}

void loop() {

  com.run();
  
//  myServo.write(0); // 0도
//  delay(1000);
//  myServo.write(90); // 90도
//  delay(1000);
//  myServo.write(180); // 180도
//  delay(1000);
//  myServo.write(90); // 90도
//  delay(1000);
  

}
```



### 키패드를 이용한 도어락 구현

EEPROM으로 비밀먼호 저장하는 과정이 있으니!! 참고하자

```c++
//키패드 라이브러리 사용
#include <EEPROM.h>
#include <Keypad.h>
#include <SimpleTimer.h>
#include <Servo.h>
#include <Led.h>

const byte ROWS = 4; // 4행
const byte COLS = 4; // 4열

const byte col_pins[COLS] = {9,8,7,6};// 키패드 연결(C1~C4)
const byte row_pins[ROWS] = {10,11,12,13};// 키패드 행 연결(R1~R4)

const char hex_keys[ROWS][COLS] ={    // 키 이름
  {'0','1','2','3'},
  {'4','5','6','7'},
  {'8','9','A','B'},
  {'C','D','E','F'}
};

                    // map 함수로 불러옴(행렬) , row,col에서          행렬값2개로 위치값리턴
Keypad myKeypad = Keypad(makeKeymap(hex_keys), row_pins, col_pins, ROWS, COLS); // 키패드 객체 생성
String input ="";
bool b_press = false; // 첫번째 입력인지 여부 판단.
SimpleTimer timer;
int timer_id;

Servo door;
String PASSWORD = "";
Led led(4); //부저와 같이 동작

// 키를입력하면 input에 구성
// F를 누르면 완료
// 이때까지 입력한 문자열을 시리얼로 출력

String read_password(){
  String password ="";
  for(int i=0; i<20; i++){
    char ch = EEPROM.read(i);
    if(ch=='\0'){  // 문자열의 끝이면 중지
        break;
     }
     password +=ch;
  }
  return password;
}
void write_password(String password){
  int size = password.length();
    for(int i =0; i< size; i++){
        char c = password[i];
        Serial.println(c);
        EEPROM.write(i,c);
    }
    EEPROM.write(size,'\0'); // 문자열의 끝 기록 문자열 끝을 나타내는 NULL 기록
}

void beep(int interval =100){
  led.on();
  delay(interval);
  led.off();
}

void setup() {
  Serial.begin(9600);
  door.attach(5);
  door.write(0);
  led.off();
  PASSWORD = read_password(); // 저장된 비밀번호 읽어오기
  
}

void reset(){
  Serial.println("Time out! reset");
  input = "";
  b_press = false;
  for(int i=0 ;i<3; i++){
    beep();
    delay(200);
  }
}





void open_door(){
  //서보모터로 문을열어줌
  beep(200);
  door.write(90);
  Serial.println("Welcom to Entering");
  delay(5000);
  //서보 모터로 문을 닫음
  door.write(0);
  beep(200);
}


void loop() {
  timer.run();
  char key = myKeypad.getKey();
                                      // settimeout으로 마지막키 누르고 n초이후 확인안하면 리셋
                                      // restarttime으로 버튼누를때 대기시간 초기화

  if(key != NO_KEY){                // 시리얼 모니터 눌린 키 이름 출력
    beep();
    if(key != 'F'){                 //키 입력중
        if(!b_press){               // 첫 번째 키 입력이면..
          //타이머 setTimeout
          timer_id = timer.setTimeout(3000,reset);
          b_press = true;
        }else{  //타이머 restart
          timer.restartTimer(timer_id);          
        }
        input +=key;
    }else{  // 키 입력완료됨
        timer.deleteTimer(timer_id);
        Serial.print("input: ");   // 일정시간 지나면 리셋해보리겠다.
        Serial.println(input);      // println이기때문에 string처럼 가져온다
    
      // 'C'로 시작하면 새로운 비밀번호 저장
      // 'C0248'이면 '0248' 로 새로운비밀번호 설정
      if(input[0] =='C'){
        PASSWORD =input.substring(1); // substring(from,to) to 는 미포함
        write_password(PASSWORD);
        Serial.println("New password: " +PASSWORD);
      }else{
        if(input ==PASSWORD){
          //문을 열어줌
          Serial.println("Password Matched!");
          open_door();
        }else{
          Serial.println("Password Mismatched!");
          beep(1000);
        }
      } 
      input = "";
      b_press = false;
    }
  }
}

```

