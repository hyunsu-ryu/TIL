# 20.DC모터 구동(차량) & 아두이노 블루투스 통신



## 1) DC모터 구동

- 모터 A,B 연결선이 있으며, 배터리 연결선ena,enb, in1,in2로 구성됨
- GND선에는 모터부분과, 아두이노부분을 동시에 접합해준뒤 꽃아준다
- ENA,ENB
  - 점퍼스위치가 있는 경우 정속도(속도조절x)
  - 점퍼스위치 제거 후 PWM 핀에 연결하면 속도 조절 가능

## 2) 블루투스 연동

- Serial과 아두이노의는 반대기
  - 아두이노에서 TX- 11번, RX -10번
  - 회로(시리얼)에서는 TX -10번 , RX -11번으로 인식한다 
  - 즉 아두이노에서는 값줄때 TX(11), RX(10)으로 데이터 주는데
  - 회로는 RX(11), TX(10)으로 반대로 아두이노에게 다시 데이터 넘겨준다.

- SoftwareSerial 라이브러리 (시리얼과 사용방법 동일)
  - SoftwareSerial()   - 생성자
  - available()  - 수신 데이터 존재 여부
  - begin()    - 전송 속도 설정
  - listen()    - 마스터 모드에서 접속 대기
  - read()  - 데이터 읽기
  - print() - 데이터 출력
  - println() - 데이터 출력
  - write() - 데이터 출력

- 동작 확인
  - 시리얼 모니터 입력창
    - Both NL&CR 선택하며
    - AT 입력해준다 정상이면 OK 답변이 오게됨
- 버전확인
  - AT+VERSION
- 이름변경
  - AT+NAMEHyunsu
- PIN(비밀번호) 설정
  - AT+PIN1234

---

- 생성해줘야 하는 h파일, cpp 파일
- DcMotor.h

```c++
#pragma once

#include <Arduino.h>

class DcMotor{
protected:    
    int in1;
    int in2;
    int en;
    int speed;
    
public:
    DcMotor(int in1, int in2, int en);
    int getSpeed(){return speed;}
    void setSpeed(int speed);
    void forward();
    void backward();
    void stop();
};
```

- DcMotor.cpp

```c++
#include "DcMotor.h"

DcMotor::DcMotor(int in1, int in2, int en):
    in1(in1), in2(in2), en(en){

    speed =0;
    pinMode(in1,OUTPUT);
    pinMode(in2,OUTPUT);
    pinMode(en,OUTPUT);
}

void DcMotor::setSpeed(int speed){
    this ->speed = speed;
    analogWrite(en,speed);
}

void DcMotor::forward(){
    digitalWrite(in1,HIGH);
    digitalWrite(in2,LOW);
}

void DcMotor::backward(){
    digitalWrite(in1,LOW);
    digitalWrite(in2,HIGH);

}

void DcMotor::stop(){
    digitalWrite(in1,LOW);
    digitalWrite(in2,LOW);
}
```

- Robot.h

```c++
#pragma once

#include <DcMotor.h>

class Robot{
protected:
    DcMotor l_motor;
    DcMotor r_motor;
    int speed;
public:
    Robot(int in1, int in2, int ena,    // left_motor용
          int in3, int in4, int enb);   //right_motor용
    
    int getSpeed() {return speed;}
    void setSpeed(int speed);
    void forward();
    void backward();
    void stop();
    void turn_left();
    void turn_right();
};
```

- Robot.cpp

```c++
#include "Robot.h"

Robot::Robot(int in1, int in2, int ena, int in3, int in4 ,int enb):
    l_motor(in1, in2, ena), r_motor(in3, in4, enb){

        speed =0;
}

void Robot::setSpeed(int speed){
    this->speed = speed;
    l_motor.setSpeed(speed);
    r_motor.setSpeed(speed);

}
void Robot::forward(){
    l_motor.forward();
    r_motor.forward();

}
void Robot::backward(){
    l_motor.backward();
    r_motor.backward();

}
void Robot::stop(){
    l_motor.stop();
    r_motor.stop();

}
void Robot::turn_left(){
    l_motor.backward();     //backward();
    r_motor.forward();

}
void Robot::turn_right(){
    l_motor.forward();
    r_motor.backward();


}
```

- BtMiniCom.h

```c++
#pragma once

#include <Arduino.h>
#include <SoftwareSerial.h>
#include <MiniCom.h>

typedef void (*btminicom_callback_t)(String message); // 수신된 메시지 있을때 얘를 호출함

class BtMiniCom : public MiniCom{ // Minicom에서 상속받음
protected:
    SoftwareSerial btSerial;
    btminicom_callback_t callback; // 메시지 수신 시 호출할 콜백 함수 포인터

public:     // 기능확장
    BtMiniCom(int rx, int tx, btminicom_callback_t callback); // rx, tx로 btSerial을 초기화하며, callback으로 포인터화
    void init();
    String readLine();          // \r \n를 제외한 문자열 리턴
    void send(String msg);  // 메시지 전송
    void run();

};

```

- BtMiniCom.cpp

```c++
#include "BtMiniCom.h"

BtMiniCom::BtMiniCom(int rx, int tx, btminicom_callback_t callback)
    :MiniCom(),btSerial(rx,tx), callback(callback){
        // btMiniCom으로 부모의 생성자 호출

}

void BtMiniCom::init(){     // init으로 오버라이드해줌
    MiniCom::init();        // minicom 초기화
    btSerial.begin(9600);   // 소프트웨어 시리얼 초기화
}


// 개행문자 제시될때까지 읽는녀석
String BtMiniCom::readLine(){  // \r\n을 제외한 문자열 리턴, 수신 없으면 빈거 리턴해줌
    String message ="";
    while(btSerial.available()){
        char data =(char)btSerial.read();
        if(data =='\r') continue;
        if(data =='\n') break;

        message +=data;
        delay(5);               // 수신 문자열 끊김 방지*******
    }                       // 루프 도는속도가 serial수신보다 빠르기에 abc 칠때 a치고 끈길수있으니 b가 오게 기다려주는시간줌
    return message;
}

void BtMiniCom::send(String msg){  // *msg 하면 string에 대한 포인터 변수가 되버린다.
    btSerial.println(msg);
}

void BtMiniCom::run(){          //메시지가 비어있는 비어잇는 문자열이 아닐시 콜백해줘라
    String msg = readLine();
    if(msg !="" && callback !=NULL){
        callback(msg);
    }
    MiniCom::run();  // 부모클래스를 run 호출함
}
```

---

- 본격적인 예제

- 시리얼통신을 이용한 차량바퀴 제어 기본예제

```c++
//int ENPin =9; // enable핀 설정
//int in1Pin =8; // int1핀 설정
//int in2Pin = 7; // in2핀 설정

int ENPin =3; // enable핀 설정
int in1Pin =5; // int1핀 설정
int in2Pin = 4; // in2핀 설정


void setup() {
  // 시리얼 통신 설정
  Serial.begin(9600);

  //핀 입출력 설정
  pinMode(ENPin,OUTPUT);
  pinMode(in1Pin,OUTPUT);
  pinMode(in2Pin,OUTPUT);

  analogWrite(ENPin, 200); //PWM제어 ... 0~255

}

void loop() {
  Serial.println("Forward");
  analogWrite(ENPin,255);   // 속도 제어!
  digitalWrite(in1Pin,HIGH);
  digitalWrite(in2Pin,LOW);
  delay(1000);

  Serial.println("Stop");
  digitalWrite(in1Pin,LOW);
  digitalWrite(in2Pin,LOW);
  delay(1000);

  Serial.println("Back");
  analogWrite(ENPin,100) ;
  digitalWrite(in1Pin,LOW);
  digitalWrite(in2Pin,HIGH);
  delay(1000);

  Serial.println("Stop");
  digitalWrite(in1Pin,LOW);
  digitalWrite(in2Pin,LOW);
  delay(1000);

  
}
```



- 로봇class로 받아와서 바퀴 조절하는예제, 아두이노에 값을 저장할 수 있다.

```c++
#include <Robot.h>


Robot car(8,7,9,5,4,3);



void setup() {
  car.stop();
  car.setSpeed(200);

}

void loop() {
  car.forward();
  delay(1000);
  car.stop();
  delay(500);

  car.backward();
  delay(1000);
  car.stop();
  delay(500);

  car.turn_left();
  delay(1000);
  car.stop();
  delay(500);

  car.turn_right();
  delay(1000);
  car.stop();
  delay(500);

}
```

- **기기와 블루투스 페어링을 통해 아두이노 내장함수에 명령어를 줘서 구동시키는 예제**

```c++
#include <BtMiniCom.h>
#include <Robot.h>

void receive(String msg);

Robot car(8,7,9,5,4,3);
BtMiniCom com(10,11,receive);

// 블루투스 수신 메시지 처리함수
void receive(String msg){
  com.print(0, "Command: ");
  com.print(1, msg.c_str());

  if(msg =="f"){
    car.forward();
  }else if(msg=="b"){
    car.backward();
  }else if(msg=="s"){
    car.stop();
  }else if(msg=="l"){
    car.turn_left();
  }else if(msg=="r"){
    com.send("UnKnown Command");
  }
  
}



void setup() {
  car.stop();
  car.setSpeed(200);

}

void loop() {
  com.run();

}
```

