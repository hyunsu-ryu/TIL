# WiFi 통신모듈(NodeMCU) / NodeMCU MQTT라이브러리

- 오픈소스 사물인터넷 (IoT) 플랫폼
- 와이파이 기능이 구현된 MCU 개발보드
- ESPRESSIF 사의 ESP8266-12 모듈을 사용

- 3.3V 로 동작

- 주요핀
  - D3,D4,D8 : 부팅모드를 설정하기 위해 내정
  - D0 : Sleep mode에서 벗어나기 위한 Wake용
  - 사용에 제한없는핀 : D1(SCL), D2(SDL) // D5,D6,D7
  - 내장LED : D4

- 개발환경 설정
  - 파일 -> 환경설정
    - 추가적인 보드 매니저에 URL을 추가해준다.

---

- **MqttCom** 
  - WiFi 접속 및 Mqtt 처리 클래스
    - MiniCom을 상속
  - 주요 메서드
    - MqttCom(const char *ssid, const char *password)
      - 생성자
    - void wifi_connect();
      - WiFi 연결
    - void init(const char *server, const char *topic =NULL, MQTT_CALLBACK_SIGNATURE =NULL);
    - void reconnect();
      - Mqtt 서버 연결
    - void run();
      - 기본 운영 및 Mqtt 이벤트 처리
- MqttCom.h

```c++
#pragma once
#include <MiniCom.h>
#include <ESP8266WiFi.h>
#include <PubSubClient.h>

class MqttCom: public MiniCom {
protected:
    const char *server;     //  MQTT서버 주소
    const char *ssid;       // 네트워크 ID
    const char *password;   // 네트워크 비밀번호
    String client_id;       // MQTT클라이언트 ID
    WiFiClient espClient;   // WiFi 객체
    PubSubClient client;    // MQTT 클라이언트 객체
    const char *topic;      // 구독할 토픽 명
    MQTT_CALLBACK_SIGNATURE;    // 구독 콜백 함수 포인터
    // void(*callback)(char*,uin8_t*,unsigned int);임

public :
    MqttCom(const char *ssid, const char *password);
    void wifi_connect();
    void init(const char *server, const char *topic = NULL,
                MQTT_CALLBACK_SIGNATURE = NULL);
    void reconnect();
    void run();
    void publish(const char *topic, const char *value);


};
```



- MqttCom.cpp

```c++
#include "MqttCom.h"


MqttCom::MqttCom(const char *ssid, const char *password) :
    MiniCom(115200), ssid(ssid), password(password), client(espClient) {
    topic = NULL;
    callback = NULL;
    server = NULL;
    randomSeed(analogRead(0));
    int r = random(300);
    client_id = String("ESP8266Client") + r;    // 랜덤하게 클라이언트 아이디 설정
}   

void MqttCom::wifi_connect() {
    WiFi.begin(ssid, password);
    while (WiFi.status() != WL_CONNECTED) {
        delay(500);
        Serial.print(".");
    }

    Serial.println("");
    Serial.println("WiFi connected");
    Serial.println(WiFi.localIP());

    lcd.setCursor(0, 0);
    lcd.print("WiFi connected");
    lcd.setCursor(0, 1);
    lcd.print(WiFi.localIP());
}

void MqttCom::init(const char *server, const char *topic,
                    MQTT_CALLBACK_SIGNATURE) {
    this->server = server;
    this->callback = callback;
    this->topic = topic;

    MiniCom::init();
    wifi_connect();
    
    client.setServer(server, 1883);

    if(callback != NULL) {
            client.setCallback(callback);
    }
}


void MqttCom::reconnect() {
    while (!client.connected()) {
        Serial.print("Attempting MQTT connection...");
        print(0, "try MQTT con...");
        if (client.connect(client_id.c_str())) { // 클라이언트 ID 중복 주의
            Serial.println("connected");
            print(1, "connected");

            if(topic != NULL) {
            client.subscribe(topic);
            }

        } else {
            char buf[17];
            sprintf(buf, "failed, rc=%d", client.state());
            Serial.print(buf);
            print(0, buf);
            Serial.println(" try again in 5 seconds");
            print(1, "try again in 5 sec");
            delay(5000);
        }
    }
}   


void MqttCom::run() {
    MiniCom::run();

    if (!client.connected()) {
        reconnect();
    }
    client.loop();
}

void MqttCom::publish(const char *topic, const char *value){
    client.publish(topic,value);
}
```



---



- Wifi 연결 테스트

```c++
/*
  ESP8266 Blink by Simon Peter
  Blink the blue LED on the ESP-01 module
  This example code is in the public domain

  The blue LED on the ESP-01 module is connected to GPIO1
  (which is also the TXD pin; so we cannot use Serial.print() at the same time)

  Note that this sketch uses LED_BUILTIN to find the pin with the internal LED
*/

void setup() {
  pinMode(LED_BUILTIN, OUTPUT);     // Initialize the LED_BUILTIN pin as an output
}

// the loop function runs over and over again forever
void loop() {
  digitalWrite(LED_BUILTIN, LOW);   // Turn the LED on (Note that LOW is the voltage level
  // but actually the LED is on; this is because
  // it is active low on the ESP-01)
  delay(1000);                      // Wait for a second
  digitalWrite(LED_BUILTIN, HIGH);  // Turn the LED off by making the voltage HIGH
  delay(2000);                      // Wait for two seconds (to demonstrate the active low LED)
}
```

- 버튼을 눌렀을때 led가 blink되고 한번 더 누를시 led꺼지게 만들기 

```c++
// 버튼을 눌렀을 때
//led가 blink 되게 하고
//한번 더 눌렀을 때
//led가 꺼지게

#include <MiniCom.h>
#include <Led.h>
#include <Button.h>

// NodeMCU 핀번호 지정시 주의사항 ****
Button btn(D5); // 5 : GPI05를 의미함, D5로 표기해야함
Led led(D6);   // 6: GPI06을 의미함, D6으로 표기해야함

MiniCom com;
bool state = false; // true : blink모드, false: off 모드
int timer_id =-1;   // off 모드를 실행시키기 위해서!

void check(){
    if(!state){
      com.setInterval(500,[](){    //0.5초 주기로 체크를해라
        com.print(0,"Blink mode");
        led.toggle(); 				// 반대로 만들어라!
      });
      state = true;
    }else{
      led.off();
      state = false;
      com.print(0,"Off mode");
      // 타이머를꺼야함 없으면 interval에 바로들어감 
      SimpleTimer& timer = com.getTimer(); // &없으면 timer가 다를 수 있음,
      timer.deleteTimer(timer_id);        // 원본에 접근할것
    }
}

void setup() {
  com.init();
  btn.setCallback(check);
  com.print(0,"Off mode");
}

void loop() {
  btn.check();
  com.run();

}
```

- 온도 습도센서 Minicom 나타내는 예제 (차후에 응용되니 미리 알아두자)

```c++
//온도습도센서스
#include <DHT.h>
#include <MiniCom.h>

DHT dht11(D5,DHT11); // 2초이상의 시간 간격으로 측정, 더 짧으면 에러남.
MiniCom com;
double temp =0;
double humi =0;

void check(){
  temp = dht11.readTemperature();
  humi = dht11.readHumidity();

  com.print_d(0,"Temp. :",temp);
  com.print_d(1,"Humi. :",humi);
  
}

void setup() {
  com.init();
  dht11.begin();  // 빼먹지않게 조심하기
  com.setInterval(2000,check);			// 2초에 한번씩 체크해라

}

void loop() {
  com.run();

}
```

- **Wifi 무선네트워크를 통해서 Serial에 나타내기** 
  - **주의사항 : 5GHz 연결안됨 2GHz에만 연결하기** 
  - **ESP8266WiFi 내장 라이브러리 설치 후 사용하기**
  - 홈페이지에 IP 주소 나타내면 Helloworld 보여주
  - 와이파이 주소로써 wifi모듈과 노트북내 Serial과 연결한다.

```c++
#include <ESP8266WiFi.h>    //wifi 모듈 쓰기위해 헤더 적어줌
#include <MiniCom.h>        // 일반적으로 웹으로 운영하진 않는데 알고있자

//5GHz 연결안됨 2GHz에 연결
const char *ssid = "KT_GiGA_5G_Wave2_EF71";      // 무선네트워크 ID
const char *password = "62zd11jj22";  // 비밀번호 

MiniCom com(115200); // 시리얼 속도 디폴트 9600 에서 115200으로 수정해줌
WiFiServer server(80);

void wifi_connect(){
  WiFi.begin(ssid,password);
  while(WiFi.status() != WL_CONNECTED){
    delay(500);
    Serial.print(".");
  }
  com.print(0, "WiFi connected",true); // ture면 시리얼 에도 같이 출력
  IPAddress ip = WiFi.localIP();
  String ipstr = ip.toString(); //문자열 리턴해줌
  com.print(1, ipstr.c_str(), true);
}


void setup() {
  com.init();
  WiFi.mode(WIFI_STA); // STAND ALONE 모드
  wifi_connect();
  server.begin();

}

void loop() {
  WiFiClient client = server.available();//
  if(!client){  // 접속이 없으면 바로 리턴
    return;
  }

  // Wait until the client sends some data
  Serial.println("new client");
  while(!client.available()){
    delay(1);
  }

  //이 방법으로 하면 생각보다 작업량이 많다.
  //Read the first line of the request
  String request = client.readStringUntil('\r');
  Serial.println(request);
  client.flush();

  //Return the response
  client.println("HTTP/1.1 200 OK");
  client.println("Content-Type: text/html");   //응답헤더
  client.println("");                       // 헤더와 바디 구분역할
  client.println("<!DOCTYPE HTML>");        // 응답 바디
  client.println("<html>");
  client.print("HELLO WORLD!");
  client.println("</html>");
  delay(1);
  Serial.println("Client disconnected");
  Serial.println("");
  
  

}
```



---

- **MQTT_IP_Server**
  - PubSubClient 라이브러리 다운받기
  - 명령프롬포트로 통신 주고받기 가능
  - mosquitto_sub -h localhost -t outTopic
  - mosquitto_pub -h localhost -t inTopic -m 1
  - mosquitto_pub -h localhost -t inTopic -m 0 으로 구분
- mosquitto_sub -h localhost -t outTopic 을 통해 Hello world 출력가능

```c++
#include <ESP8266WiFi.h>
#include <PubSubClient.h>
#include <MiniCom.h>

const char *ssid = "KT_GiGA_2G_Wave2_EF71";      // 무선네트워크 ID
const char *password = "62zd11jj22";  // 비밀번호 
const char* mqtt_server = "172.30.1.58"; // 노트북의 iv4 아이피주소 



MiniCom com(115200);
WiFiClient espClient;
PubSubClient client(espClient);
long lastMsg =0;
char msg[50];
int value =0;

void wifi_connect(){
  WiFi.begin(ssid,password);
  while(WiFi.status() != WL_CONNECTED){
    delay(500);
    Serial.print(".");
  }
  com.print(0,"WiFi connected",true);
  IPAddress ip = WiFi.localIP();
  String ipstr = ip.toString();
  com.print(1, ipstr.c_str(),true);
}



void setup() {
  pinMode(BUILTIN_LED,OUTPUT);
  Serial.begin(115200);
  wifi_connect();
  client.setServer(mqtt_server,1883);
  client.setCallback(callback); // 토픽 수신 시 호출할 함수 등록

}

void callback(char* topic, byte* payload, unsigned int length){
  Serial.print("Message arrived [");
  Serial.print(topic);
  Serial.print("]");
    for(int i=0 ; i<length ; i++){
      Serial.print((char)payload[i]);
    }
    Serial.println();
  
    if((char)payload[0] =='1'){
      digitalWrite(BUILTIN_LED,LOW);
      
    }else{
      digitalWrite(BUILTIN_LED,HIGH);
    }
  
}

void reconnect() {
    // Loop until we're reconnected
    while (!client.connected()) {
      Serial.print("Attempting MQTT connection...");
      // Attempt to connect
      if (client.connect("ESP8266Client")) { // 여러대 운영시 클라이언트 ID 중복 주의
          Serial.println("connected");
          // Once connected, publish an announcement...
          client.publish("outTopic", "hello world"); // 보내는사람
          // ... and resubscribe
          client.subscribe("inTopic");			 // 받는사람
      } else {
        Serial.print("failed, rc=");
        Serial.print(client.state());
        Serial.println(" try again in 5 seconds");
        // Wait 5 seconds before retrying
        delay(5000);
      }
    }
}





void loop() {
    if (!client.connected()) {
      reconnect();
    }
    client.loop();
  
    long now = millis();
    if (now - lastMsg > 2000) {// 2초 간격으로 publish
        lastMsg = now;
        ++value;
        snprintf (msg, 75, "hello world #%ld", value);
        Serial.print("Publish message: ");
        Serial.println(msg);
        client.publish("outTopic", msg);
    }
}
```



- MQTT_classModule
  - mosquitto_pub -h localhost -t test/led -m on
  - mosquitto_pub -h localhost -t test/led -m off
  - mosquitto_pub -h localhost -t test/led -m blink
  - mosquitto_pub -h localhost -t test/led -m toggle

```c++
#include <MqttCom.h>

const char *password = "62zd11jj22";  // 비밀번호 
const char* server = "172.30.1.58";
const char *sub_topic = "test/led";  // sub하는 아이디 이름이라 생각하자 pub가 명령창을통해 이 아이디에게 보내줌
// test/#. test/+  는 지원이 안된다(한계존재)


MqttCom com(ssid,password);

// 제어하기 위해서
void subscribe(char* topic, uint8_t* payload, unsigned int length){
  char buf[128];            
  memcpy(buf,payload,length); //메모리를 복사함 lengh 길이만큼!(루프 돌리지않고 복사해주는함수
  buf[length] = '\0';

  Serial.println(topic);
  Serial.println(buf);    // 문자열기반의 통신!
}

// 센서로 외부 특정값의 데이터를 전송
void publish(){
  com.publish("test/greet", "hello world!");
}

//명령창에서 sub 들어가면 pub한녀석의 자료를 받아온다!
// 정수라도 ->변환해서 문자열로 데이터를 주고받는다 

void setup() {
  // com.init(server); // publish만 하겠다는 경우
    com.init(server, sub_topic, subscribe); // subscribe도 하는 경우
    com.setInterval(2000,publish);
}

void loop() {
  com.run();

}
```

