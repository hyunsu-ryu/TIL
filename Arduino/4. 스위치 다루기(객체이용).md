# 3.3 스위치 다루기

- 앞의 장과 동일하게 Led.h / Led.cpp를 이용한다
- 그리고 Button에 대한 클래스를 새로 정의하며 Button.h/ Button.cpp를 통해 버튼의 행위를 메서드를 통해지정하게 된다.
- Led.h

```c++
#pragma once   // 한번만 include 되게 보장해주는 녀석
#include <Arduino.h> //output High 정의하고있는 헤더파일

class Led{
protected:
  int pin;   // 연결할 핀 번호

public:
  Led(int pin);

  void on();
  void off();
  void setValue(int value);
  void toggle(); // 현재상태를 반전해주는것
};

```

- Led.cpp

```c++
#include "Led.h"

Led::Led(int pin): pin(pin){   //멤버변수(매개변수)로 초기화 시킴
  //this ->pin = pin; 다른 초기화 방법
  pinMode(pin,OUTPUT);


}

void Led::on(){				// scope를 통해 Led 클래스의 void매서드임을 알려주고 메서드의 행위를 정의함
  digitalWrite(pin,HIGH);
}

void Led::off(){
  digitalWrite(pin,LOW);
}

void Led::setValue(int value){
  digitalWrite(pin, value);
}

void Led::toggle(){
  int v = digitalRead(pin);     // 위에 OUTPUT 모드에선 Read가능
  
  digitalWrite(pin,!v);          // 핀번호에서 v와 반대로 읽겠다.(0,1) 둘중에 왓다갓다함 
}
```

- Button.h

```c++
#pragma once

#include <Arduino.h>


// 함수에 대한 포인터를 button_callback_t로 정의
typedef void (*button_callback_t)();


class Button {
protected:
    int pin;
    // 함수에 대한 포인터
    // void (*callback)() ;  //콜백함수(함수에한 포인터) ( 버튼누를때 실행함수)
    button_callback_t callback;
    unsigned long t1;

public:
    Button(int pin);
    void setCallback(button_callback_t callback);
    void check();
    int read();
    void attachInterrupt(button_callback_t callback, int mode);
    bool debounce();
};
```

- Button.cpp

```c++
#include "Button.h"

Button::Button(int pin):pin(pin){
    pinMode(pin, INPUT_PULLUP);
    callback = NULL;
}

void Button::setCallback(button_callback_t callback){
    this -> callback = callback;
}

// 버튼이 눌러졌는지 체크
void Button::check(){
    bool o_sw, n_sw;

    o_sw =digitalRead(pin);
    delay(10);
    n_sw = digitalRead(pin);

    if(o_sw ==1 && n_sw ==0){   // 버튼을 누른 시점
        if(callback !=NULL){
            callback();
        }
        
    }
}

// 누른경우 high  뗀경우 low
int Button::read(){
    return !digitalRead(pin);   // 반대행위를 리턴하겠다. (반대로하겠다)
}

void Button::attachInterrupt(button_callback_t callback, int mode){
    ::attachInterrupt(digitalPinToInterrupt(pin), callback, mode); // 여기서 ::는 attachInterrupt() 이 Class(=Button.h)의 것이 아닌 Button.cpp의 것임을 알려준다.
    t1 = millis();
}

bool Button::debounce(){
    unsigned long t2 = millis();
    if((t2-t1)< 200 ) return false;

    t1=t2;
    return true;
}
```



- 스위치 풀업,다운 동작 (2개)

```c++
//실습 1 스위치 풀업 풀다운 동작
#include "Led.h"
Led led1(9);   // 생성할때 바로 LED를 설정해줌 전원공급!
Led led2(8);

int sw1 =3;   //풀업으로 회로를 구성한 3번
int sw2 =4;   //풀다운으로 회로를 구성한 4번

void setup() {
  pinMode(sw1,INPUT);
  pinMode(sw2,INPUT);
}

void loop() {
  int v1, v2;

  // 풀업 : 누르면 L 평소H
  v1= digitalRead(sw1);
  led1.setValue(v1);			// 시작값은 v1으로 한다 즉 5V공급되는 상황

  // 풀다운 : 누르면H, 평소L
  v2 = digitalRead(sw2);
  led2.setValue(!v2);   // 반대로 해석하게끔 만들어줌
}
```

- 풀업 상황일때 (1개)

```c++
#include <Led.h>

Led led(8);
int sw= 4;

void setup() {
  pinMode(sw,INPUT_PULLUP);

}

void loop() {
  int v = digitalRead(sw);
  led.setValue(!v);

}
```

- 스위치를 누를때: on and off

```c++
#include <Led.h>

Led led(8);
int sw = 4;

void setup() {
  pinMode(sw, INPUT_PULLUP);   // 풀업 상황에서

}

void loop() {
  bool o_sw, n_sw;
  o_sw =digitalRead(sw);
  delay(10);
  n_sw = digitalRead(sw);

  if(o_sw == 1 && n_sw == 0){   // 버튼을 누른 시점!
    led.toggle();				// toggle은 반대상황을 만들어준다.
  }

}
```

- 콜백 함수를 통한 버튼 누르기 
  - setCallback은 work의 주소값에 접근해 work 메서드를 이용하게된다.

```c++
#include <Led.h>
#include <Button.h>

Led led(8);
Button btn(4);

void work(){   // 버튼누를시 호출함수 callback의 void와 매개변수x 와 같아야
  led.toggle();
}
void setup() {
  btn.setCallback(work);  // work 함수의 시작값 주소이기 떄문에
}

void loop() {
  btn.check();

}
```

- 누를때마다 Led 순차점멸(Step)

```c++
#include <Led.h>
#include <Button.h>

Led leds[4]{
    Led(8),
    Led(9),
    Led(10),
    Led(11),
    
};
Button btn(4);
int current = 0;
//버튼 누를때마다 led 순차점멸

void work() {				// 누르면 알아서 자동으로 버튼
  for(int i=0; i<4; i++){
    if(i==current){
        leds[i].on();
     }else{
        leds[i].off();
    }
  }
  current = (current+1)%4;
}

void setup() {
  btn.setCallback(work);	//콜백으로 work 메서드에 대해 접근한다.

}

void loop() {
  btn.check();
}
```

