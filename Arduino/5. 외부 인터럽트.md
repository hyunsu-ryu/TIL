# 3.4 외부 인터럽트

- 기존 Bottom,Led.h 파일과 cpp 파일은 유지한다

- 

- 0V(Low level) - 5V(High level) 감지!

- 외부 인터럽트 감지신호

  - | 신호분류  | 신호상태                   |
    | --------- | -------------------------- |
    | LOW 레벨  | OFF                        |
    | 하강 엣지 | OFF --> ON ( 누를 때 신호) |
    | 상승 엣지 | ON --> OFF  (땔때 신호)    |
    | 상태 변화 | OFF --> ON 또는 ON -->OFF  |

- void attachInterrupt(digitalPinTolnterrupt(pin, ISR, mode))

  - ...(pin)하고 (그외 쓰고싶은 함수, 메서드 쓰면됨)

  - 인터럽트 서비스 루틴임

  - 모드 종류

    | LOW     | 핀에 입력되는 신호가 LOW일때           |
    | ------- | -------------------------------------- |
    | CHANGE  | 핀에 입력되는신호가 변할때(L->H, H->L) |
    | RISING  | LOW에서 HIGH로 변할 때(상승엣지)       |
    | FALLING | HIGH에서 LOW로 변할 때(하강엣지)       |

  

- 외부 인터렙트 처리 함수 (LED등 2개일때)

```c++
// switch 눌려질때마다 on/off 외부 인터럽트사용
const int sw_pin =3;
const int led_pin =8;
volatile boolean led_st =HIGH;   // 초기상태 (OFF)풀다운! 접지이용
volatile unsigned long t1,t2;

// 외부 인터럽트1 처리 함수
void flash(void){
  // 디 바운싱 처리( 체터링 잡아줌)
  t2 =millis();
  if((t2-t1)<200) return;		// 체터링 체크함
    else t1=t2;
  
  led_st = !led_st;             // LED 상태 반전
  digitalWrite(led_pin, led_st); // LED 상태 반전 출력
}


void setup() {
  pinMode(sw_pin,INPUT_PULLUP);
  // 외부인터럽트 설정
  attachInterrupt(digitalPinToInterrupt(sw_pin), flash, FALLING); 
									// pin번호 , 함수, 이벤트감지!
  pinMode(led_pin, OUTPUT);     // LED 연결핀 출력 설정
  digitalWrite(led_pin,led_st); // LED 초기상태 출력

  t1 =millis();
}

void loop() {
  // put your main code here, to run repeatedly:

}
```

- 외부인터랩트(LED 한개일때)

```c++
#include <Led.h>
#include <Button.h>

Button btn(3);
Led led(8);


void flash(){
  if(btn.debounce()){			// debounce를 통해 버튼의 행위를 결정!!
      led.toggle();      // 참일 경우 내가 하고싶은거 한다.
  }
}

void setup() {
  btn.attachInterrupt(flash,FALLING);  // flash를 구성해서 함수를 넣어준다.sw_pin안넣은건 Button btn(3); 에서 다 처리해줬기 때문
  //t1 = millis();
  

}

void loop() {
  

}
```

- 인터랩트를 통한 버튼 번누를때마다 LED 불 들어오는 것 다르게 하기

```c++
//4개 LED 순차 점멸
#include <Led.h>
#include <Button.h>

Button btn(3);
Led leds[4]{		// led 들들어가는곳?
    Led(8),
    Led(9),
    Led(10),		// 포트 여러개표기
    Led(11),
    
};
int current =0;
void flash() {
  if(!btn.debounce())return;
    
    for(int i=0; i<4; i++){
      if(i ==current){
        leds[i].on();
      }else{
        leds[i].off();
      }
    }
    current = (current+1)%4;
  
}

void setup(){
  btn.attachInterrupt(flash,FALLING);
}
void loop() {
  // put your main code here, to run repeatedly:

}
```



- 연습문제 : 누를때마다 step 점멸속도 다르게하기

```c++
#include <Led.h>
#include <Button.h>

Button btn(3);
Led leds[4]{
    Led(8),
    Led(9),
    Led(10),
    Led(11)

};

int intervals[4] {200, 400, 600, 800}; // 조종할 interval 값 배열인덱스로 접근
int current =0;     //현재 커져야할 Led인덱스
int intervalIx =0; // 현재 운영할 interval의 인덱스


void blink(){					//점멸하는거! 조정
  for(int i=0; i<4; i++){
    if(i==current){
        leds[i].on();
        
    }else{
        leds[i].off();
    }
  }
  current = (current+1)%4;
}

void flash() {					// 버튼누를때 일어나는 행위를 나타내는 함수
  if(!btn.debounce()) return;	// delay에 적용하기위해 인덱스 적용
  intervalIx = (intervalIx+1)%4;
}

void setup() {
  btn.attachInterrupt(flash,FALLING);	//

}

void loop() {
  blink();
  delay(intervals[intervalIx]);			//여기서 delay적용!

}
```



---

기본 헤더파일들 올림

- Led.h

```c++
#pragma once   // 한번만 include 되게 보장해주는 녀석
#include <Arduino.h> //output High 정의하고있는 헤더파일

class Led{
protected:
  int pin;   // 연결할 핀 번호

public:
  Led(int pin);

  void on();
  void off();
  void setValue(int value);
  void toggle(); // 현재상태를 반전해주는것
};

```

- Led.cpp

```c++
#include "Led.h"

Led::Led(int pin): pin(pin){   //멤버변수(매개변수)로 초기화 시킴
  //this ->pin = pin; 다른 초기화 방법
  pinMode(pin,OUTPUT);


}

void Led::on(){
  digitalWrite(pin,HIGH);
}

void Led::off(){
  digitalWrite(pin,LOW);
}

void Led::setValue(int value){
  digitalWrite(pin, value);
}

void Led::toggle(){
  int v = digitalRead(pin);     // 위에 OUTPUT 모드에선 Read가능
  
  digitalWrite(pin,!v);           
}
```

- Button.h

```c++
#pragma once

#include <Arduino.h>


// 함수에 대한 포인터를 button_callback_t로 정의
typedef void (*button_callback_t)();


class Button {
protected:
    int pin;
    // 함수에 대한 포인터
    // void (*callback)() ;  //콜백함수(함수에한 포인터) ( 버튼누를때 실행함수)
    button_callback_t callback;
    unsigned long t1;

public:
    Button(int pin);
    void setCallback(button_callback_t callback);
    void check();
    int read();
    void attachInterrupt(button_callback_t callback, int mode);
    bool debounce();
};
```

- Button.cpp

```c++
#include "Button.h"

Button::Button(int pin):pin(pin){
    pinMode(pin, INPUT_PULLUP);
    callback = NULL;
}

void Button::setCallback(button_callback_t callback){
    this -> callback = callback;
}

// 버튼이 눌러졌는지 체크
void Button::check(){
    bool o_sw, n_sw;

    o_sw =digitalRead(pin);
    delay(10);
    n_sw = digitalRead(pin);

    if(o_sw ==1 && n_sw ==0){   // 버튼을 누른 시점
        if(callback !=NULL){
            callback();
        }
        
    }
}

// 누른경우 high  뗀경우 low
int Button::read(){					//그것을 읽어온다!
    return !digitalRead(pin);
}

void Button::attachInterrupt(button_callback_t callback, int mode){
    ::attachInterrupt(digitalPinToInterrupt(pin), callback, mode);
    t1 = millis();
}

bool Button::debounce(){			//버튼 누를때 채터링일어나지않게!
    unsigned long t2 = millis();
    if((t2-t1)< 200 ) return false;

    t1=t2;
    return true;
}
```

